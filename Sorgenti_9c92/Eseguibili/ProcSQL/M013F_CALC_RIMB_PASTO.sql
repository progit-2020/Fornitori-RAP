CREATE OR REPLACE FUNCTION M013F_CALC_RIMB_PASTO(TIPORISULTATO IN VARCHAR2, INCODICE IN VARCHAR2, TIPOREGISTRAZIONE IN VARCHAR2,
                                                 DATADA IN DATE,DATAA IN DATE,
                                                 ORADA IN VARCHAR2, ORAA IN VARCHAR2) RETURN NUMBER IS
/*
Restituisce il valore del rimborso pasto oppure il numero pasti della regola
in base al TIPORISULTATO richiesto:
- "I" = importo rimborso
- "N" = numero pasti
*/
  CURSOR C1 IS
    --Estrazione regole rimborso pasto
    SELECT M013.*, M013.ROWID
      FROM M013_SOGLIE_RIMBORSIPASTO M013
     WHERE M013.CODICE = INCODICE
       AND M013.TIPO_MISSIONE = TIPOREGISTRAZIONE
       AND DATADA BETWEEN M013.DECORRENZA AND M013.DECORRENZA_FINE
     ORDER BY M013.SOGLIA_GG DESC;

  RESULT NUMBER;
  NGG NUMBER;
  DURATA NUMBER;
  MAXIMPORTO NUMBER;
  MAXNUM NUMBER;
  ORERIMBORSOPASTO VARCHAR2(5);
  ORERIMBORSOPASTO2 VARCHAR2(5);
  TIPO_RIMBORSOPASTO VARCHAR2(1);
  TARIFFARIMBORSOPASTO NUMBER;
  ARROTONDAMENTOORE NUMBER(8);
  TIPO VARCHAR2(1);
  M013_NREGOLE NUMBER;
BEGIN
  RESULT:=0;
  --Estrazione regole trasferta
  BEGIN
    SELECT M010.ORERIMBORSOPASTO, M010.TARIFFARIMBORSOPASTO,
           M010.ORERIMBORSOPASTO2, TRUNC(M010.ARROTONDAMENTOORE),M010.TIPO,
           M010.TIPO_RIMBORSOPASTO
      INTO ORERIMBORSOPASTO, TARIFFARIMBORSOPASTO, ORERIMBORSOPASTO2,
           ARROTONDAMENTOORE, TIPO, TIPO_RIMBORSOPASTO
      FROM M010_PARAMETRICONTEGGIO M010
     WHERE M010.DECORRENZA = (SELECT MAX(T.DECORRENZA)
                                 FROM M010_PARAMETRICONTEGGIO T
                                WHERE T.DECORRENZA <= DATAA
                                  AND T.TIPO_MISSIONE = M010.TIPO_MISSIONE
                                  AND T.CODICE = M010.CODICE)
       AND M010.TIPO_MISSIONE = TIPOREGISTRAZIONE
       AND M010.CODICE = INCODICE;

    SELECT COUNT(*) INTO M013_NREGOLE
      FROM M013_SOGLIE_RIMBORSIPASTO M013
     WHERE M013.CODICE = INCODICE
       AND M013.TIPO_MISSIONE = TIPOREGISTRAZIONE
       AND DATADA BETWEEN M013.DECORRENZA AND M013.DECORRENZA_FINE;
    --se non trovo alcun record regole corrispondente sulla m010 esco
    --se non trovo regole sia sulla m010 sia sulla m013 allora esco
    IF TIPO_RIMBORSOPASTO IN ('0','1') AND (M013_NREGOLE = 0) THEN
      RETURN -1;
    END IF;
    IF (TIPO_RIMBORSOPASTO = '2') AND (TARIFFARIMBORSOPASTO IS NULL) THEN
      RETURN -1;
    END IF;
  EXCEPTION
  WHEN NO_DATA_FOUND THEN
    RETURN -1;
  END;
  /*testo la presenza del codice rimborso pasto
    e quindi della necessità del calcolo*/
  NGG:=DATAA - DATADA;
  MAXIMPORTO:=0;
  IF TIPO_RIMBORSOPASTO = '0' THEN  
    --gestione scaglioni per rimborso max
    --trasferta in giornata
    MAXNUM:=-1;
    IF NGG = 0 THEN
      DURATA:=OREMINUTI(ORAA) - OREMINUTI(ORADA);--da arrotondare
      DURATA:=Arrotonda(DURATA,ARROTONDAMENTOORE,TIPO);
      FOR T1 IN C1 LOOP
        IF DURATA >= OREMINUTI(T1.SOGLIA_GG) THEN
          MAXIMPORTO:=T1.RIMBORSO_MAX;
          EXIT;
        END IF;
      END LOOP;
    ELSIF NGG >= 1 THEN
      --trasferta su più giorni
      --1° giorno
      DURATA:=1440 - OREMINUTI(ORADA); --da arrotondare
      DURATA:=Arrotonda(DURATA,ARROTONDAMENTOORE,TIPO);
      FOR T1 IN C1 LOOP
        IF DURATA >= OREMINUTI(T1.SOGLIA_GG) THEN
          MAXIMPORTO:=T1.RIMBORSO_MAX;
          EXIT;
        END IF;
      END LOOP;
      --ultimo giorno
      DURATA:=OREMINUTI(ORAA); --da arrotondare
      DURATA:=Arrotonda(DURATA,ARROTONDAMENTOORE,TIPO);
      FOR T1 IN C1 LOOP
        IF DURATA >= OREMINUTI(T1.SOGLIA_GG) THEN
          MAXIMPORTO:=MAXIMPORTO + T1.RIMBORSO_MAX;
          EXIT;
        END IF;
      END LOOP;
      --giorni interi
      DURATA:=1440;
      FOR T1 IN C1 LOOP
        IF DURATA >= OREMINUTI(T1.SOGLIA_GG) THEN
          MAXIMPORTO:=MAXIMPORTO + T1.RIMBORSO_MAX * (NGG - 1);
          EXIT;
        END IF;
      END LOOP;
    END IF;
  ELSIF TIPO_RIMBORSOPASTO = '1' THEN
    --gestione CUMULO scaglioni per rimborso max
    --trasferta in giornata
    MAXNUM:=-1;
    IF NGG = 0 THEN
      DURATA:=OREMINUTI(ORAA) - OREMINUTI(ORADA);--da arrotondare
      DURATA:=Arrotonda(DURATA,ARROTONDAMENTOORE,TIPO);
      FOR T1 IN C1 LOOP
        IF DURATA >= OREMINUTI(T1.SOGLIA_GG) THEN
          MAXIMPORTO:=T1.RIMBORSO_MAX;
          EXIT;
        END IF;
      END LOOP;
    ELSIF NGG >= 1 THEN
      --primo + ultimo giorno
      DURATA:=1440 - OREMINUTI(ORADA) + OREMINUTI(ORAA); --da arrotondare
      DURATA:=Arrotonda(DURATA,ARROTONDAMENTOORE,TIPO);
      NGG:=NGG + trunc(DURATA/1440);
      DURATA:=mod(DURATA,1440);
      FOR T1 IN C1 LOOP
        IF DURATA >= OREMINUTI(T1.SOGLIA_GG) THEN
          MAXIMPORTO:=MAXIMPORTO + T1.RIMBORSO_MAX;
          EXIT;
        END IF;
      END LOOP;
      --giorni interi
      DURATA:=1440;
      FOR T1 IN C1 LOOP
        IF DURATA >= OREMINUTI(T1.SOGLIA_GG) THEN
          MAXIMPORTO:=MAXIMPORTO + T1.RIMBORSO_MAX * (NGG - 1);
          EXIT;
        END IF;
      END LOOP;    
    END IF;     
  ELSIF TIPO_RIMBORSOPASTO = '2' THEN
    --COMUNE DI TORINO - gestione fasce orarie per rimborso max
    --trasferta in giornata
    MAXNUM:=0;
    IF NGG = 0 THEN
      IF (ORERIMBORSOPASTO IS NOT NULL) AND (OREMINUTI(ORERIMBORSOPASTO) BETWEEN OREMINUTI(ORADA) AND OREMINUTI(ORAA)) THEN
        MAXIMPORTO:=MAXIMPORTO + TARIFFARIMBORSOPASTO;
        MAXNUM:=MAXNUM + 1;
      END IF;
      IF (ORERIMBORSOPASTO2 IS NOT NULL) AND (OREMINUTI(ORERIMBORSOPASTO2) BETWEEN OREMINUTI(ORADA) AND OREMINUTI(ORAA)) THEN
        MAXIMPORTO:=MAXIMPORTO + TARIFFARIMBORSOPASTO;
        MAXNUM:=MAXNUM + 1;
      END IF;
    ELSIF NGG >= 1 THEN
      --trasferta su più giorni
      --1° giorno
      IF (ORERIMBORSOPASTO IS NOT NULL) AND (OREMINUTI(ORERIMBORSOPASTO) BETWEEN OREMINUTI(ORADA) AND 1440) THEN
        MAXIMPORTO:=MAXIMPORTO + TARIFFARIMBORSOPASTO;
        MAXNUM:=MAXNUM + 1;
      END IF;
      IF (ORERIMBORSOPASTO2 IS NOT NULL) AND (OREMINUTI(ORERIMBORSOPASTO2) BETWEEN OREMINUTI(ORADA) AND 1440) THEN
        MAXIMPORTO:=MAXIMPORTO + TARIFFARIMBORSOPASTO;
        MAXNUM:=MAXNUM + 1;
      END IF;
      --giorni interi
      IF (ORERIMBORSOPASTO IS NOT NULL) THEN
        MAXIMPORTO:=MAXIMPORTO + (TARIFFARIMBORSOPASTO * (NGG - 1));
        MAXNUM:=MAXNUM + (NGG - 1);
      END IF;
      IF (ORERIMBORSOPASTO2 IS NOT NULL) THEN
        MAXIMPORTO:=MAXIMPORTO + (TARIFFARIMBORSOPASTO * (NGG - 1));
        MAXNUM:=MAXNUM + (NGG - 1);
      END IF;
      --ultimo giorno
      IF (ORERIMBORSOPASTO IS NOT NULL) AND (OREMINUTI(ORERIMBORSOPASTO) BETWEEN 0 AND OREMINUTI(ORAA)) THEN
        MAXIMPORTO:=MAXIMPORTO + TARIFFARIMBORSOPASTO;
        MAXNUM:=MAXNUM + 1;
      END IF;
      IF (ORERIMBORSOPASTO2 IS NOT NULL) AND (OREMINUTI(ORERIMBORSOPASTO2) BETWEEN 0 AND OREMINUTI(ORADA)) THEN
        MAXIMPORTO:=MAXIMPORTO + TARIFFARIMBORSOPASTO;
        MAXNUM:=MAXNUM + 1;
      END IF;
    END IF;
  END IF;
  -- restituisce il valore espresso come importo oppure come numero
  IF TIPORISULTATO = 'I' THEN
    RESULT:=MAXIMPORTO;
  ELSIF TIPORISULTATO = 'N' THEN
    RESULT:=MAXNUM;
  END IF;
  RETURN(RESULT);
END M013F_CALC_RIMB_PASTO;
/