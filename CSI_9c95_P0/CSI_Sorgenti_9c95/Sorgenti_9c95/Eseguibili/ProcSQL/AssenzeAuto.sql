CREATE OR REPLACE PROCEDURE ASSENZEAUTO (PROG IN NUMBER, DAL IN DATE, AL IN DATE) IS
  TYPE TGIORNI IS TABLE OF DATE INDEX BY BINARY_INTEGER;
  GIORNI TGIORNI;
  NUMNONLAV NUMBER(2);
  I NUMBER(2);
  DATACORR DATE;
  NTIMB NUMBER;
  NGIUS NUMBER;
  LAV VARCHAR2(1);
  FEST VARCHAR2(1);
  COD VARCHAR2(5);
  TGEST VARCHAR2(1);
  RIC VARCHAR2(1);
  INSERVIZIO INTEGER;
BEGIN
  DATACORR:=DAL;
  NUMNONLAV:=0;
  WHILE DATACORR <= AL LOOP
    -- CONTROLLO SE ESISTONO TIMBRATURE E GIUSTIFICATIVI NEL GIORNO CORRENTE
    NTIMB:=0;
    NGIUS:=0;
    SELECT COUNT(*) INTO NTIMB FROM T100_TIMBRATURE
     WHERE PROGRESSIVO = PROG AND DATA = DATACORR;
    IF NTIMB = 0 THEN
      SELECT COUNT(*) INTO NGIUS FROM T040_GIUSTIFICATIVI
       WHERE PROGRESSIVO = PROG AND DATA = DATACORR;
    END IF;
    SELECT COUNT(*) INTO INSERVIZIO FROM T430_STORICO 
     WHERE PROGRESSIVO = PROG AND DATACORR BETWEEN INIZIO AND NVL(FINE,DATACORR);
    IF (NTIMB = 0) AND (NGIUS = 0) AND (INSERVIZIO > 0) THEN
      -- CONTROLLO CHE IL GIORNO PRECEDENTE CI SIA UNA GIORNATA DI ASSENZA CON CAUSALE RICORSIVA
      BEGIN
        SELECT CAUSALE INTO COD FROM T040_GIUSTIFICATIVI T040,T265_CAUASSENZE T265
         WHERE PROGRESSIVO = PROG
           AND DATA = DATACORR - 1
           AND TIPOGIUST = 'I'
           AND T040.CAUSALE = T265.CODICE
           AND T265.RICORSIONE = 'S';
         NGIUS:=1;
      EXCEPTION
        WHEN NO_DATA_FOUND THEN
          NGIUS:=0;
      END;
      IF NGIUS > 0 THEN
        LAV:=NULL;
        FEST:=NULL;
        BEGIN
        -- RICERCA GIORNO SU CALENDARIO INDIVIDUALE
        SELECT LAVORATIVO,FESTIVO INTO LAV,FEST FROM T012_CALENDINDIVID
         WHERE PROGRESSIVO = PROG
           AND DATA = DATACORR;
        EXCEPTION
          WHEN NO_DATA_FOUND THEN
            BEGIN
            -- RICERCA GIORNO SU CALENDARIO GENERALE
            SELECT LAVORATIVO,FESTIVO INTO LAV,FEST FROM T011_CALENDARI
             WHERE CODICE =
                   (SELECT CALENDARIO FROM T430_STORICO
                     WHERE PROGRESSIVO = PROG
                       AND DATACORR BETWEEN DATADECORRENZA AND DATAFINE)
               AND DATA = DATACORR;
            EXCEPTION
              WHEN NO_DATA_FOUND THEN
                DATACORR:=AL;
                LAV:=NULL;
                FEST:=NULL;
            END;
        END;
        IF (LAV IS NOT NULL) AND (FEST IS NOT NULL) THEN
          -- GIORNO NON LAVORATIVO
          IF (LAV = 'N') OR (FEST = 'S') THEN
            BEGIN
            -- LEGGO SE PERSONALE NORMALE O TURNISTA
              SELECT TGESTIONE INTO TGEST FROM T430_STORICO
               WHERE PROGRESSIVO = PROG
                 AND DATACORR BETWEEN DATADECORRENZA AND DATAFINE;
            EXCEPTION
              WHEN NO_DATA_FOUND THEN
                TGEST:='0';
            END;
            IF TGEST = '0' THEN
              NUMNONLAV:=NUMNONLAV + 1;
              GIORNI(NUMNONLAV):=DATACORR;
            END IF;
          ELSE
            -- GIORNO LAVORATIVO
            NUMNONLAV:=0;
          END IF;
          INSERT INTO T040_GIUSTIFICATIVI
            (PROGRESSIVO,DATA,CAUSALE,PROGRCAUSALE,TIPOGIUST)
            VALUES
            (PROG,DATACORR,COD,0,'I');
          COMMIT;
        END IF;
      END IF;
    ELSE
      FOR I IN 1..NUMNONLAV LOOP
        DELETE FROM T040_GIUSTIFICATIVI
         WHERE PROGRESSIVO = PROG
           AND DATA = GIORNI(I);
      END LOOP;
      NUMNONLAV:=0;
      COMMIT;
    END IF;
    DATACORR:=DATACORR + 1;
  END LOOP;
END ASSENZEAUTO;
/
